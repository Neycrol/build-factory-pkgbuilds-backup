# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pacman-contrib
pkgver=0.r0.g0000000
pkgrel=1
pkgdesc='Contributed scripts and tools for pacman systems'
arch=(x86_64)
url=https://gitlab.archlinux.org/pacman/pacman-contrib
license=(GPL-2.0-or-later)
depends=(pacman)
makedepends=(
  asciidoc
  git
)
optdepends=(
  'diffutils: for pacdiff'
  'fakeroot: for checkupdates'
  'findutils: for pacdiff --find'
  'mlocate: for pacdiff --locate'
  'plocate: faster mlocate alternative'
  'perl: for pacsearch'
  'sudo: privilege elevation for several scripts'
  'vim: default diff program for pacdiff'
  'neovim: default diff program for pacdiff if EDITOR=nvim'
)
source=("git+$url.git")
b2sums=('SKIP')
validpgpkeys=(04DC3FB1445FECA813C27EFAEA4F7B321A906AD9 44A62127789A949F2F082ED429C71CE612B57264) # Daniel M. Capella <polyzen@archlinux.org>
#             5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5)  # Johannes Löthberg <johannes@kyriasis.com>

pkgver() {
  cd $pkgname
  printf "%s.r%s.g%s" \
    "$(git show -s --format=%cd --date=format:%Y%m%d HEAD)" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
