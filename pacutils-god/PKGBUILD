# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pacutils
pkgver=0.r0.g0000000
pkgrel=2
pkgdesc='Helper tools for libalpm'
url='https://github.com/andrewgregory/pacutils'
arch=('x86_64')
license=('MIT')
depends=(
  'glibc'
  'libarchive'
  'pacman'
)
makedepends=('git' 'perl')
checkdepends=('perl')
source=(
  "git+https://github.com/andrewgregory/pacutils.git"
  '0001-pacutils-pacman-7.1-compatibility.patch'
  '0002-Support-new-DisableSandbox-configuration-options.patch'
)
sha256sums=('SKIP'
            'b9956e9593f27c354998b5728a534756ef7c2fd222eee36664d7f8a2d3a21093'
            '00acaa2e2d41324e351d9850deefc40b349a8251b125c298d6451144a13f64f1')
validpgpkeys=('0016846EDD8432261C62CB63DF3891463C352040')

pkgver() {
  cd pacutils
  printf "%s.r%s.g%s" \
    "$(git show -s --format=%cd --date=format:%Y%m%d HEAD)" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd pacutils

  # https://github.com/andrewgregory/pacutils/pull/83
  git apply -3 ../0001-pacutils-pacman-7.1-compatibility.patch
  git apply -3 ../0002-Support-new-DisableSandbox-configuration-options.patch
}

build() {
  cd pacutils
  make CFLAGS="$CFLAGS $LDFLAGS" SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

# #check() {
#   cd pacutils
#   make check
# }

package() {
  cd pacutils
  make DESTDIR="$pkgdir" PREFIX=/usr install-bin install-lib
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}

# vim: set ft=PKGBUILD et sw=2:
